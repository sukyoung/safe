/**
 * *****************************************************************************
 * Copyright (c) 2016-2018, KAIST.
 * All rights reserved.
 *
 * Use is subject to license terms.
 *
 * This distribution may include materials developed by third parties.
 * ****************************************************************************
 */

/*
 * A.3 Expressions
 * Definition of JavaScript expressions
 */
module kr.ac.kaist.safe.parser.Expression(Declaration, ExpressionNoIn, Literal,
                                          Symbol, Identifier, Keyword, Spacing);

import Declaration;
import ExpressionNoIn;
import Literal;
import Symbol;
import Identifier;
import Keyword;
import Spacing;

/*
 * Expression ::= Assignment (w , w Assignment)*
 */
public Expr Expression =
     a1:Assignment a2s:(w void:comma w Assignment)*
     { if (a2s.list().isEmpty()) yyValue = a1;
       else yyValue = new ExprList(NodeUtil.makeASTNodeInfo(createSpan(yyStart,yyCount)),
                                   toList(cons(a1, a2s.list())));
     };

/*
 * Assignment ::=
 *   Conditional
 * | LeftHandSide w = w Assignment
 * | LeftHandSide w AssignmentOp w Assignment
 */
Expr Assignment =
     a1:LeftHandSide w a2:equals w a3:Assignment
     { Span span = createSpan(yyStart,yyCount);
       yyValue = new AssignOpApp(NodeUtil.makeASTNodeInfo(span), a1, a2, a3);
     }
   / a1:LeftHandSide w a2:AssignmentOp w a3:Assignment
     { Span span = createSpan(yyStart,yyCount);
       yyValue = new AssignOpApp(NodeUtil.makeASTNodeInfo(span), a1, a2, a3);
     }
   / Conditional;

/*
 * Conditional ::=
 *   LogicalOR
 * | LogicalOR w ? w Assignment w : w Assignment
 */
private transient Expr Conditional =
     a1:LogicalOR w question w a2:Assignment w colon w a3:Assignment
     { Span span = createSpan(yyStart,yyCount);
       yyValue = new Cond(NodeUtil.makeASTNodeInfo(span), a1, a2, a3);
     }
   / LogicalOR ;

/*
 * LogicalOR ::=
 *   LogicalAND
 * | LogicalOR w || w LogicalAND
 */
private transient Expr LogicalOR =
     a1:LogicalAND a2s:LogicalANDR*
     { Span span = a1.span();
       yyValue = a1;
       for (Tuple2<Op, Expr> pair: a2s.list()) {
           Op op = pair._1();
           Expr right = pair._2();
           yyValue = new InfixOpApp(NodeUtil.makeASTNodeInfo(Span.merge(span, right.span())),
                                    yyValue, op, right);
       }
     };

private transient Tuple2<Op, Expr> LogicalANDR =
     w a1:bars w a2:LogicalAND
     { yyValue = new Tuple2<Op, Expr>(a1, a2); };

/*
 * LogicalAND ::=
 *   BitwiseOR
 * | LogicalAND w && w BitwiseOR
 */
private transient Expr LogicalAND =
     a1:BitwiseOR a2s:BitwiseORR*
     { Span span = a1.span();
       yyValue = a1;
       for (Tuple2<Op, Expr> pair: a2s.list()) {
           Op op = pair._1();
           Expr right = pair._2();
           yyValue = new InfixOpApp(NodeUtil.makeASTNodeInfo(Span.merge(span, right.span())),
                                    yyValue, op, right);
       }
     };

private transient Tuple2<Op, Expr> BitwiseORR =
     w a1:ands w a2:BitwiseOR
     { yyValue = new Tuple2<Op, Expr>(a1, a2); };

/*
 * BitwiseOR ::=
 *   BitwiseXOR
 * | BitwiseOR w | w BitwiseXOR
 */
private transient Expr BitwiseOR =
     a1:BitwiseXOR a2s:BitwiseXORR*
     { Span span = a1.span();
       yyValue = a1;
       for (Tuple2<Op, Expr> pair: a2s.list()) {
           Op op = pair._1();
           Expr right = pair._2();
           yyValue = new InfixOpApp(NodeUtil.makeASTNodeInfo(Span.merge(span, right.span())),
                                    yyValue, op, right);
       }
     };

private transient Tuple2<Op, Expr> BitwiseXORR =
     w a1:bar w a2:BitwiseXOR
     { yyValue = new Tuple2<Op, Expr>(a1, a2); };

/*
 * BitwiseXOR ::=
 *   BitwiseAND
 * | BitwiseXOR w ^ w BitwiseAND
 */
private transient Expr BitwiseXOR =
     a1:BitwiseAND a2s:BitwiseANDR*
     { Span span = a1.span();
       yyValue = a1;
       for (Tuple2<Op, Expr> pair: a2s.list()) {
           Op op = pair._1();
           Expr right = pair._2();
           yyValue = new InfixOpApp(NodeUtil.makeASTNodeInfo(Span.merge(span, right.span())),
                                    yyValue, op, right);
       }
     };

private transient Tuple2<Op, Expr> BitwiseANDR =
     w a1:caret w a2:BitwiseAND
     { yyValue = new Tuple2<Op, Expr>(a1, a2); };

/*
 * BitwiseAND ::=
 *   Equality
 * | BitwiseAND w & w Equality
 */
private transient Expr BitwiseAND =
     a1:Equality a2s:EqualityR*
     { Span span = a1.span();
       yyValue = a1;
       for (Tuple2<Op, Expr> pair: a2s.list()) {
           Op op = pair._1();
           Expr right = pair._2();
           yyValue = new InfixOpApp(NodeUtil.makeASTNodeInfo(Span.merge(span, right.span())),
                                    yyValue, op, right);
       }
     };

private transient Tuple2<Op, Expr> EqualityR =
     w a1:and w a2:Equality
     { yyValue = new Tuple2<Op, Expr>(a1, a2); };

/*
 * Equality ::=
 *   Relational
 * | Equality w == w Relational
 * | Equality w != w Relational
 * | Equality w StrictEquals w Relational
 * | Equality w !== w Relational
 */
private transient Expr Equality =
     a1:Relational a2s:RelationalR*
     { Span span = a1.span();
       yyValue = a1;
       for (Tuple2<Op, Expr> pair: a2s.list()) {
           Op op = pair._1();
           Expr right = pair._2();
           yyValue = new InfixOpApp(NodeUtil.makeASTNodeInfo(Span.merge(span, right.span())),
                                    yyValue, op, right);
       }
     };

private transient Tuple2<Op, Expr> RelationalR =
     w a1:EqualityOp w a2:Relational
     { yyValue = new Tuple2<Op, Expr>(a1, a2); };

/*
 * Relational ::=
 *   Shift
 * | Relational w < w Shift
 * | Relational w > w Shift
 * | Relational w <= w Shift
 * | Relational w >= w Shift
 * | Relational w instanceof w Shift
 * | Relational w in w Shift
 */
private transient Expr Relational =
     a1s:ShiftL+ a2:Shift
     { List<Tuple2<Expr, Op>> ps = a1s.list();
       Tuple2<Expr, Op> first = ps.remove(0);
       Expr left = first._1();
       Op op = first._2();
       Span span = left.span();
       for (Tuple2<Expr, Op> pair: ps) {
           Expr e = pair._1();
           left = new InfixOpApp(NodeUtil.makeASTNodeInfo(Span.merge(span, e.span())),
                                 left, op, e);
           op = pair._2();
       }
       yyValue = new InfixOpApp(NodeUtil.makeASTNodeInfo(createSpan(yyStart,yyCount)), left, op, a2);
     }
   / Shift ;

private transient Tuple2<Expr, Op> ShiftL =
     a1:Shift w a2:RelationalOp w
     { yyValue = new Tuple2<Expr, Op>(a1, a2); };

/*
 * Shift ::=
 *   Additive
 * | Shift w << w Additive
 * | Shift w >> w Additive
 * | Shift w >>> w Additive
 */
transient Expr Shift =
     a1s:AdditiveL+ a2:Additive
     { List<Tuple2<Expr, Op>> ps = a1s.list();
       Tuple2<Expr, Op> first = ps.remove(0);
       Expr left = first._1();
       Op op = first._2();
       Span span = left.span();
       for (Tuple2<Expr, Op> pair: ps) {
           Expr e = pair._1();
           left = new InfixOpApp(NodeUtil.makeASTNodeInfo(Span.merge(span, e.span())),
                                 left, op, e);
           op = pair._2();
       }
       yyValue = new InfixOpApp(NodeUtil.makeASTNodeInfo(createSpan(yyStart,yyCount)), left, op, a2);
     }
   / Additive ;

private transient Tuple2<Expr, Op> AdditiveL =
     a1:Additive w a2:ShiftOp w
     { yyValue = new Tuple2<Expr, Op>(a1, a2); };

/*
 * Additive ::=
 *   Multiplicative
 * | Additive w + w Multiplicative
 * | Additive w - w Multiplicative
 */
private transient Expr Additive =
     a1s:MultiplicativeL+ a2:Multiplicative
     { List<Tuple2<Expr, Op>> ps = a1s.list();
       Tuple2<Expr, Op> first = ps.remove(0);
       Expr left = first._1();
       Op op = first._2();
       Span span = left.span();
       for (Tuple2<Expr, Op> pair: ps) {
           Expr e = pair._1();
           left = new InfixOpApp(NodeUtil.makeASTNodeInfo(Span.merge(span, e.span())),
                                 left, op, e);
           op = pair._2();
       }
       yyValue = new InfixOpApp(NodeUtil.makeASTNodeInfo(createSpan(yyStart,yyCount)), left, op, a2);
     }
   / Multiplicative ;

private transient Tuple2<Expr, Op> MultiplicativeL =
     a1:Multiplicative w a2:AdditiveOp w
     { yyValue = new Tuple2<Expr, Op>(a1, a2); };

/*
 * Multiplicative ::=
 *   Unary
 * | Multiplicative w * w Unary
 * | Multiplicative w / w Unary
 * | Multiplicative w % w Unary
 */
private transient Expr Multiplicative =
     a1s:UnaryL+ a2:Unary
     { List<Tuple2<Expr, Op>> ps = a1s.list();
       Tuple2<Expr, Op> first = ps.remove(0);
       Expr left = first._1();
       Op op = first._2();
       Span span = left.span();
       for (Tuple2<Expr, Op> pair: ps) {
           Expr e = pair._1();
           left = new InfixOpApp(NodeUtil.makeASTNodeInfo(Span.merge(span, e.span())),
                                 left, op, e);
           op = pair._2();
       }
       yyValue = new InfixOpApp(NodeUtil.makeASTNodeInfo(createSpan(yyStart,yyCount)), left, op, a2);
     }
   / Unary ;

private transient Tuple2<Expr, Op> UnaryL =
     a1:Unary w a2:MultiplicativeOp w
     { yyValue = new Tuple2<Expr, Op>(a1, a2); };

/*
 * Unary ::=
 *   Postfix
 * | delete Unary
 * | void Unary
 * | typeof Unary
 * | ++ Unary
 * | -­ Unary
 * | + Unary
 * | ­ Unary
 * | ~ Unary
 * | ! Unary
 */
transient Expr Unary =
     a1:delete w a2:Unary
     { Span span = createSpan(yyStart,yyCount);
       yyValue = new PrefixOpApp(NodeUtil.makeASTNodeInfo(span), a1, a2);
     }
   / a1:void w a2:Unary
     { Span span = createSpan(yyStart,yyCount);
       yyValue = new PrefixOpApp(NodeUtil.makeASTNodeInfo(span), a1, a2);
     }
   / a1:typeof w a2:Unary
     { Span span = createSpan(yyStart,yyCount);
       yyValue = new PrefixOpApp(NodeUtil.makeASTNodeInfo(span), a1, a2);
     }
   / a1:doubleplus w a2:Unary
     { Span span = createSpan(yyStart,yyCount);
       yyValue = new PrefixOpApp(NodeUtil.makeASTNodeInfo(span), a1, a2);
     }
   / a1:doubleminus w a2:Unary
     { Span span = createSpan(yyStart,yyCount);
       yyValue = new PrefixOpApp(NodeUtil.makeASTNodeInfo(span), a1, a2);
     }
   / a1:plus w a2:Unary
     { Span span = createSpan(yyStart,yyCount);
       yyValue = new PrefixOpApp(NodeUtil.makeASTNodeInfo(span), a1, a2);
     }
   / a1:minus w a2:Unary
     { Span span = createSpan(yyStart,yyCount);
       yyValue = new PrefixOpApp(NodeUtil.makeASTNodeInfo(span), a1, a2);
     }
   / a1:tilde w a2:Unary
     { Span span = createSpan(yyStart,yyCount);
       yyValue = new PrefixOpApp(NodeUtil.makeASTNodeInfo(span), a1, a2);
     }
   / a1:bang w a2:Unary
     { Span span = createSpan(yyStart,yyCount);
       yyValue = new PrefixOpApp(NodeUtil.makeASTNodeInfo(span), a1, a2);
     }
   / Postfix;

/*
 * Postfix ::=
 *   LeftHandSide
 * | LeftHandSide s ++
 * | LeftHandSide s --
 */
private transient Expr Postfix =
     a1:LeftHandSide s a2:doubleplus
     { Span span = createSpan(yyStart,yyCount);
       yyValue = new UnaryAssignOpApp(NodeUtil.makeASTNodeInfo(span), a1, a2);
     }
   / a1:LeftHandSide s a2:doubleminus
     { Span span = createSpan(yyStart,yyCount);
       yyValue = new UnaryAssignOpApp(NodeUtil.makeASTNodeInfo(span), a1, a2);
     }
   / LeftHandSide;

/*
 * LeftHandSide ::=
 *   NewExpr
 * | CallExpr
 */
public LHS LeftHandSide =
     CallExpr
   / NewExpr
   ;

/*
 * CallExpr ::=
 *   MemberExpr w Args
 * | CallExpr w Args
 * | CallExpr w [ w Expression w ]
 * | CallExpr w . w IdName
 */
LHS CallExpr =
    seed:CallExprFront list:CallExprTail*
    { yyValue = (LHS)apply(list, seed); };

LHS CallExprFront =
     a1:MemberExpr w a2:Args
     { Span span = createSpan(yyStart,yyCount+1);
       yyValue = new FunApp(NodeUtil.makeASTNodeInfo(NodeUtil.adjustCallSpan(span, a1)),
                            a1, toList(a2));
     };

constant transient Action<LHS> CallExprTail =
     FunApp
   / Bracket
   / Dot
   ;

constant inline Action<LHS> FunApp =
     w a1:Args
     { yyValue = new Action<LHS>() {
             public LHS run(LHS base) {
                 SourceLoc loc = base.span().begin();
                 Span last;
                 if (a1.isEmpty()) last = base.span();
                 else last = a1.get(a1.size()-1).span();
                 Span span = new Span(base.span().fileName(),
                                               loc.line(), last.end().line(),
                                               loc.column(), last.end().column()+1,
                                               base.info().span().begin().offset(),
                                               last.end().offset()+1);
                 return new FunApp(NodeUtil.makeASTNodeInfo(span), (LHS)base, toList(a1));
             }};
     };

constant inline Action<LHS> Bracket =
     w opensquare w a1:Expression w closesquare
     { yyValue = new Action<LHS>() {
             public LHS run(LHS base) {
                 SourceLoc loc = base.span().begin();
                 Span span = new Span(base.span().fileName(),
                                               loc.line(), a1.info().span().end().line(),
                                               loc.column()+1, a1.span().end().column()+1,
                                               base.info().span().begin().offset(),
                                               a1.info().span().end().offset()+1);
                 return new Bracket(NodeUtil.makeASTNodeInfo(span), (LHS)base, a1);
             }};
     };

constant inline Action<LHS> Dot =
     w dot w a1:IdName
     { yyValue = new Action<LHS>() {
             public LHS run(LHS base) {
                 return new Dot(NodeUtil.makeASTNodeInfo(Span.merge(base.span(),
                                                                              a1.span())),
                                (LHS)base, a1);
             }};
     };

/*
 * Args ::= ( (w ArgList)? w )
 */
transient List<Expr> Args =
     openparen w closeparen
     { yyValue = list(); }
   / openparen w yyValue:ArgList w closeparen ;

/*
 * ArgList ::= Assignment (w , w Assignment)*
 */
private transient List<Expr> ArgList =
     a1:Assignment a2s:(w void:comma w Assignment)*
     { yyValue = cons(a1, a2s.list()); } ;

/*
 * NewExpr ::=
 *   MemberExpr
 * | new w NewExpr
 */
private transient LHS NewExpr =
     MemberExpr
   / new w a1:NewExpr
     { Span span = createSpan(yyStart,yyCount);
       yyValue = new New(NodeUtil.makeASTNodeInfo(span), a1);
     };

/*
 * MemberExpr ::=
 *   PrimaryExpr
 * | FunctionExpr
 * | MemberExpr w [ w Expression w ]
 * | MemberExpr w . w IdName
 * | new w MemberExpr w Args
 */
LHS MemberExpr =
    seed:MemberExprFront list:MemberExprTail*
    { yyValue = (LHS)apply(list, seed); };

LHS MemberExprFront =
     FunctionExpr
   / PrimaryExpr
   / new w a1:MemberExpr w a2:Args
     { Span span = createSpan(yyStart,yyCount+1);
       ASTNodeInfo info = NodeUtil.makeASTNodeInfo(span);
       yyValue = new New(NodeUtil.makeASTNodeInfo(span),
                         new FunApp(info, a1, toList(a2)));
     };

constant transient Action<LHS> MemberExprTail =
     MemberBracket
   / MemberDot
   ;

constant inline Action<LHS> MemberBracket =
     w opensquare w a1:Expression w closesquare
     { yyValue = new Action<LHS>() {
             public LHS run(LHS base) {
                 SourceLoc loc = base.span().begin();
                 Span span = new Span(base.span().fileName(), loc.line(),
                                               a1.info().span().end().line(),
                                               loc.column()+1, a1.span().end().column()+1,
                                               base.info().span().begin().offset(), a1.info().span().end().offset()+1);
                 return new Bracket(NodeUtil.makeASTNodeInfo(span), (LHS)base, a1);
             }};
     };

constant inline Action<LHS> MemberDot =
     w dot w a1:IdName
     { yyValue = new Action<LHS>() {
             public LHS run(LHS base) {
                 return new Dot(NodeUtil.makeASTNodeInfo(Span.merge(base.span(),
                                                                              a1.span())),
                                (LHS)base, a1);
             }};
     };

/*
 * PrimaryExpr ::=
 *   this
 * | Literal
 * | Id
 * | ArrayLiteral
 * | ObjectLiteral
 * | ( w Expression w )
 */
private transient LHS PrimaryExpr =
     this
     { Span span = createSpan(yyStart,yyCount);
       yyValue = new This(NodeUtil.makeASTNodeInfo(span));
     }
   / Literal
   / a1:Id
     { Span span = createSpan(yyStart,yyCount);
       yyValue = new VarRef(NodeUtil.makeASTNodeInfo(span), a1);
     }
   / ArrayLiteral
   / ObjectLiteral
   / openparen w a1:Expression w closeparen
     { Span span = createSpan(yyStart,yyCount+1);
       yyValue = new Parenthesized(NodeUtil.makeASTNodeInfo(span), a1);
     };

/*
 * ArrayLiteral ::=
 *   [ w (Elision w)? ]
 * | [ w ElementList w ]
 * | [ w ElementList w , w (Elision w)? ]
 */
private transient LHS ArrayLiteral =
     opensquare w a1:ElementList w comma w a2:(Elision w)? closesquare
     { Span span = createSpan(yyStart,yyCount+1);
       int elision;
       if (a2 == null) elision = 0; else elision = a2.intValue();
       List<Option<Expr>> result = new ArrayList<Option<Expr>>();
       result.addAll(a1);
       for (int i = 0; i < elision; i++)
           result.add(Option.<Expr>apply(null));
       if (writer != null) { writer.close(); initParser(); }
       yyValue = new ArrayExpr(NodeUtil.makeASTNodeInfo(span),
                               toList(result));
     }
   / opensquare w a1:ElementList w closesquare
     { Span span = createSpan(yyStart,yyCount+1);
       if (writer != null) { writer.close(); initParser(); }
       yyValue = new ArrayExpr(NodeUtil.makeASTNodeInfo(span),
                               toList(a1));
     }
   / opensquare w a1:(Elision w)? closesquare
     { Span span = createSpan(yyStart,yyCount+1);
       int elision;
       if (a1 == null) elision = 0; else elision = a1.intValue();
       List<Option<Expr>> result = new ArrayList<Option<Expr>>(elision);
       for (int i = 0; i < elision; i++)
           result.add(Option.<Expr>apply(null));
       if (writer != null) { writer.close(); initParser(); }
       yyValue = new ArrayExpr(NodeUtil.makeASTNodeInfo(span),
                               toList(result));
     };

/*
 * ElementList ::=
 *   (Elision w)? Assignment
 * | ElementList w , w (Elision w)? Assignment
 */
private transient List<Option<Expr>> ElementList =
     seed:ElementListHead list:ElementListTail*
     { yyValue = (List<Option<Expr>>)apply(list, seed); };

private transient List<Option<Expr>> ElementListHead =
     a1:(Elision w)? a2:Assignment
     { int elision;
       if (a1 == null) elision = 0; else elision = a1.intValue();
       List<Option<Expr>> result = new ArrayList<Option<Expr>>(elision);
       for (int i = 0; i < elision; i++)
           result.add(Option.<Expr>apply(null));
       yyValue = list(result, Some.<Expr>apply(a2));
     };

constant inline Action<List<Option<Expr>>> ElementListTail =
     w comma w a1:(Elision w)? a2:Assignment
     { yyValue = new Action<List<Option<Expr>>>() {
             public List<Option<Expr>> run(List<Option<Expr>> base) {
                 int elision;
                 if (a1 == null) elision = 0; else elision = a1.intValue();
                 List<Option<Expr>> result = new ArrayList<Option<Expr>>();
                 result.addAll(base);
                 for (int i = 0; i < elision; i++)
                     result.add(Option.<Expr>apply(null));
                 return list(result, Some.<Expr>apply(a2));
             }};
     };

/*
 * Elision ::= (, w)+
 */
private transient Integer Elision =
     a1s:(w comma)+ { yyValue = a1s.size(); };

/*
 * ObjectLiteral ::= { (w PropAssignList)? (w ,)? w }
 */
private transient LHS ObjectLiteral =
     opencurly a1:(w PropAssignList)? (w comma)? w closecurly
     { Span span = createSpan(yyStart,yyCount+1);
       if (a1 == null) a1 = list();
       yyValue = new ObjectExpr(NodeUtil.makeASTNodeInfo(span), toList(a1));
     };

/*
 * PropAssignList ::= PropAssign (w , w PropAssign)*
 */
private transient List<Member> PropAssignList =
     a1:PropAssign a2s:(w void:comma w PropAssign)*
     { Span span = createSpan(yyStart,yyCount);
       yyValue = cons(a1, a2s.list());
     };

/*
 * PropAssign ::=
 *   PropName w : w Assignment
 * | get w PropName w ( w ) w FunctionBody
 * | set w PropName w ( w Id w ) w FunctionBody
 */
transient Member PropAssign =
     <Getter> get w a1:PropName w openparen w closeparen w a2:FunctionBody
     { Span span = createSpan(yyStart,yyCount);
       ASTNodeInfo info = NodeUtil.makeASTNodeInfo(span);
       yyValue = new GetProp(info, a1,
                             new Functional(info, nilFds, nilVds,
                                            new Stmts(info, toList(a2._1()), a2._3()),
                                            a1.toId(), nilIds, a2._2()));
     }
   / <Setter> set w a1:PropName w openparen w a2:Id w closeparen w a3:FunctionBody
     { Span span = createSpan(yyStart,yyCount);
       ASTNodeInfo info = NodeUtil.makeASTNodeInfo(span);
       yyValue = new SetProp(info, a1,
                             new Functional(info, nilFds, nilVds,
                                            new Stmts(info, toList(a3._1()), a3._3()),
                                            a1.toId(), toList(list(a2)), a3._2()));
     }
   / <Prop> a1:PropName w colon w a2:Assignment
     { Span span = createSpan(yyStart,yyCount);
       yyValue = new Field(NodeUtil.makeASTNodeInfo(span), a1, a2);
     };

/*
 * PropName ::=
 *   IdName
 * | StringLiteral
 * | NumericLiteral
 */
transient Property PropName =
     a1:IdName
     { Span span = createSpan(yyStart,yyCount);
       yyValue = new PropId(NodeUtil.makeASTNodeInfo(span), a1);
     }
   / a1:StringLiteral
     { Span span = createSpan(yyStart,yyCount);
       yyValue = new PropStr(NodeUtil.makeASTNodeInfo(span), a1.escaped());
     }
   / a1:NumericLiteral
     { Span span = createSpan(yyStart,yyCount);
       yyValue = new PropNum(NodeUtil.makeASTNodeInfo(span), a1);
     };
